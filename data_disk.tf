resource "azurerm_managed_disk" "data_disk" {
  for_each                   = var.is_imported ? {} : var.data_disks
  name                       = lookup(var.name_overrides.data_disks, each.key, "disk-${var.virtual_machine_config.hostname}-${each.key}")
  location                   = var.virtual_machine_config.location
  resource_group_name        = var.name_overrides.resource_group_name != null ? var.name_overrides.resource_group_name : var.resource_group_name
  storage_account_type       = each.value["storage_account_type"]
  create_option              = each.value["create_option"]
  source_resource_id         = each.value["source_resource_id"]
  disk_size_gb               = each.value["disk_size_gb"]
  on_demand_bursting_enabled = each.value["on_demand_bursting_enabled"]
  zone                       = var.virtual_machine_config.zone
  disk_iops_read_write       = each.value["disk_iops_read_write"]
  disk_mbps_read_write       = each.value["disk_mbps_read_write"]
  disk_iops_read_only        = each.value["disk_iops_read_only"]
  disk_mbps_read_only        = each.value["disk_mbps_read_only"]
  max_shares                 = each.value["max_shares"] 
  trusted_launch_enabled     = each.value["trusted_launch_enabled"]

  tags = merge(
    var.tags,
    coalesce(each.value.tags, {})
  )
  lifecycle {
    prevent_destroy = true
  }
}

resource "azurerm_managed_disk" "imported" {
  for_each                   = var.is_imported ? var.data_disks : {}
  name                       = lookup(var.name_overrides.data_disks, each.key, "disk-${var.virtual_machine_config.hostname}-${each.key}")
  location                   = var.virtual_machine_config.location
  resource_group_name        = var.name_overrides.resource_group_name != null ? var.name_overrides.resource_group_name : var.resource_group_name
  storage_account_type       = each.value["storage_account_type"]
  create_option              = each.value["create_option"]
  source_resource_id         = each.value["source_resource_id"]
  disk_size_gb               = each.value["disk_size_gb"]
  on_demand_bursting_enabled = each.value["on_demand_bursting_enabled"]
  zone                       = var.virtual_machine_config.zone
  disk_iops_read_write       = each.value["disk_iops_read_write"]
  disk_mbps_read_write       = each.value["disk_mbps_read_write"]
  disk_iops_read_only        = each.value["disk_iops_read_only"]
  disk_mbps_read_only        = each.value["disk_mbps_read_only"]
  max_shares                 = each.value["max_shares"]
  trusted_launch_enabled     = each.value["trusted_launch_enabled"]

  tags = merge(
    var.tags,
    coalesce(each.value.tags, {})
  )
  lifecycle {
    prevent_destroy = true
    ignore_changes = [
      upload_size_bytes,
      create_option,
      source_resource_id 
    ]
  }
}

resource "azurerm_virtual_machine_data_disk_attachment" "data_disk" {
  for_each                  = var.data_disks
  managed_disk_id           = length(azurerm_managed_disk.data_disk) > 0 ? azurerm_managed_disk.data_disk[each.key].id : azurerm_managed_disk.imported[each.key].id
  virtual_machine_id        = var.is_imported ? azurerm_windows_virtual_machine.imported[0].id : azurerm_windows_virtual_machine.this[0].id
  lun                       = each.value["lun"]
  caching                   = each.value["caching"]
  write_accelerator_enabled = each.value["write_accelerator_enabled"]

  lifecycle {
    prevent_destroy = true
  }
}